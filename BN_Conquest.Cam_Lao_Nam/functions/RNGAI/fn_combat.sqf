//RNG AI by Toksa - Optimized for MP
params ["_unit", "_firer"];

// Only run where AI is local
if (!local _unit) exitWith {};

private _disabled = _unit getvariable ["RNG_disabled", false];
private _off = missionnamespace getvariable ["RNG_off", false];
if (isplayer _unit or _disabled or _off) exitWith {};

_unit setvariable ["RNG_incombat", true];
[_unit] spawn RNG_fnc_turning;

private _exit = false;
private _suppressed = false;
private _types = ["AGR","DFS","RND","FRM","CVR","FLW"];
if (isplayer (leader _unit)) then {_types = ["FLW","FLW","FLW","FLW","FLW","FLW"];};
private _weight = [1,0.8,0.8,0.8,0,0.8];
private _type = _types selectRandomWeighted _weight;
private _suppression = 0;
private _pos = [0,0,0];

_unit disableai "PATH";
_unit disableai "MOVE";
_unit disableai "COVER";
_unit disableai "FSM";
_unit disableai "AIMINGERROR";

private _target = objNull;
private _anims = RNG_ANIM_Tact;
private _line = [];
private _targetpos = [0,0,0];
private _infront = objNull;
private _weapon = currentweapon _unit;
private _starttime = time;
private _cancrouch = true;
private _targettimeout = time;
private _dodge = false;
private _skill = ((1 - (_unit skillfinal "general")) * 5);
private _lastObjectCheck = 0;
private _objects = [];

if (!isNull _firer && {([_unit, "FIRE", _firer] checkVisibility [aimpos _unit, aimpos _firer]) > 0}) then {_target = _firer};

if (!(unitPos _unit == "AUTO")) then {_cancrouch = false;};
sleep (random 0.5);

while {alive _unit} do {
	if (getSuppression _unit > 0.9 && {_target distance _unit > 30}) then {_cooldown=_unit getvariable ["RNG_cooldown",(time -1)]; if (time > _cooldown) then {_suppressed=true;_exit=true};};
	if (!alive _unit OR _exit OR !(vehicle _unit == _unit) OR (lifestate _unit =="INCAPACITATED") OR isplayer _unit OR (isplayer (_unit getvariable ["bis_fnc_moduleremotecontrol_owner",objNull]))) exitwith {
		[_unit,_suppressed,_cancrouch] spawn {
			_unit = _this select 0;
			_suppressed = _this select 1;
			_cancrouch=_this select 2;
			_unit playactionnow "STOP";
			dostop _unit;
			if (_suppressed) then {
			_unit setvariable ["RNG_incombat",false];
			[_unit,objNull] spawn RNG_fnc_cover;
			} else {
				dostop _unit;
				_unit domove getpos _unit;
				_unit enableai "PATH";
				_unit enableai "MOVE";
				_unit enableai "FSM";
				_unit enableai "COVER";
				_unit dofollow leader _unit;
				_unit enableai "AIMINGERROR";
				if (_cancrouch) then {_unit setunitpos "AUTO"};
				_unit setvariable ["RNG_incombat",false];
				sleep 0.1;
			};
		};
	};
	if (!(currentweapon _unit == _weapon)) then {sleep 2;_weapon = currentweapon _unit};
	if ((time - _starttime) % 5 > 4) then {
		
		_suppression=getsuppression _unit;
		if (getsuppression _unit == -1) then {
		_suppression=0;
		} else {
			_weight set [4,_suppression];
		};
		_type=_types selectRandomWeighted _weight;
	};
	if ((time - _starttime) % 30 > 29) then {
		_sortedtargets=[];
		_alltargets=_unit targets [true,300];
		_sortedtargets = [_alltargets,[],{_unit distance _x},"ASCEND",{([_unit, "FIRE",_x] checkVisibility [aimpos _unit, aimpos _x]) > 0}] call BIS_fnc_sortBy;
		sleep 0.1;
		if (!isnil "_sortedtargets" && {count _sortedtargets==0}) then {
			_exit=true;
		};
		
	};
	if ((time - _starttime) % 1 > 0.5) then {
		_alltargets=_unit targets [true,300];
		_targets = [_alltargets,[],{_unit distance _x},"ASCEND",{([_unit, "FIRE",_x] checkVisibility [aimpos _unit, aimpos _x]) > 0}] call BIS_fnc_sortBy;
		sleep 0.02;
		if (count _targets > 0) then {
			_target=_targets select 0;
			_unit dotarget _target;
			_unit dowatch _target;
			_unit lookat _target;
		_targettimeout=time + 5;
		} else {
			if ((time > _targettimeout) OR !alive _target) then {
				_target=objNull;
			};
		};
	};
	
	if ((time - _starttime) % 4 > 3.5) then {
		if (_cancrouch) then {
			_stance=selectrandom ["UP","MIDDLE"];
			if (!isNull _target && {_target distance2D _unit > 150}) then {_stance=["UP","MIDDLE","DOWN"] selectRandomWeighted [1,1,0.2];};
			if (!((unitpos _unit) == _stance)) then {
			_unit playactionnow _stance;
			};
			switch (true) do {
				case (_stance=="MIDDLE") : {_unit setunitpos "MIDDLE";};
				case (_stance=="UP") : {_unit setunitpos "UP";};
				case (_stance=="DOWN") : {_unit setunitpos "DOWN";sleep ((random 5) + 3);};
				default {};
			};
		};
	};
	// Optimized: Cache object search every 2 seconds instead of every frame
	if (time > _lastObjectCheck) then {
		_lastObjectCheck = time + 2;
		private _objectsDyn = nearestObjects [_unit, ["Wall","fence","Strategic","NonStrategic","house","Land"], 60];
		_objects = nearestTerrainObjects [_unit, ["Tree", "Bush","Wall","fence","Rock","house","Static","Thing","Building"], 60];
		_objects append _objectsDyn;
	};
	if (count _objects > 0) then {
		_targetpos=[_objects,getpos _target] call BIS_fnc_nearestPosition;
		switch (true) do {
			case (_type=="AGR"): {_targetpos=[_objects,getpos _target] call BIS_fnc_nearestPosition;};
			case (_type=="RND"): {if (count _objects > 2) then {_targetpos = _objects select 1;} else {_targetpos = _objects select 0;};};
			case (_type=="DFS"): {
				if (!isNull _target) then {
					_sortedobjects= [_objects,[],{_unit distance _x},"ASCEND",{([_target, "FIRE",_x] checkVisibility [aimpos _target, getposasl _x]) < 1}] call BIS_fnc_sortBy;
					if (count _sortedobjects > 0) then {
						_targetpos=_sortedobjects select 0;
					};
					} else {_type=_types selectRandomWeighted _weight;};
			};
			case (_type=="CVR"): {	if (!isnull _target) then {
				_sortedobjects= [_objects,[],{_unit distance _x},"ASCEND",{_target distance _x > _unit distance _x}] call BIS_fnc_sortBy;
				if (count _sortedobjects > 0) then {
					_targetpos=_sortedobjects select 0;
				};
				} else {
					_targetpos=[_objects,getpos _target] call BIS_fnc_nearestPosition;
				};};
				case (_type=="FRM"): {_targetpos=[_objects,((expectedDestination _unit) select 0)] call BIS_fnc_nearestPosition;};
				case (_type=="FLW"): {_unit dofollow leader _unit;_targetpos=[_objects,formationposition _unit] call BIS_fnc_nearestPosition;};
		};
		if (typename _targetpos == "OBJECT") then {
		_line=lineIntersectsSurfaces [[(aimpos _unit) select 0,(aimpos _unit) select 1,((aimpos _unit) select 2) - 0.5], getposASL _targetpos, _unit, objNull, true, 1,"FIRE"];
		} else {
			_line=lineIntersectsSurfaces [[(aimpos _unit) select 0,(aimpos _unit) select 1,((aimpos _unit) select 2) - 0.5],_targetpos, _unit, objNull, true, 1,"FIRE"];
		};
		if (!((count _line) == 0)) then {
			_pos=(_line select 0) select 0; 
		};
		
		////Leaning
		if ((time - _starttime) % 1 > 0.8) then {
		_center=getposasl _unit; 
		_centerPos=[_center select 0, _center select 1,(_center select 2) + 0.8];
		_leanLeft=(_unit getRelPos [0.6, 270]); 
		_leanLeftPos=AGLToASL [_leanLeft select 0, _leanLeft select 1,(_leanLeft select 2) + 0.8];
		_leanRight=(_unit getRelPos [0.6,90]); 
		_leanRightPos=AGLToASL [_leanRight select 0, _leanRight select 1,(_leanRight select 2) + 0.8];
		switch (true) do {
			case (!(pose _unit == "Lying") && {(count (lineIntersectsSurfaces [_leanLeftPos, aimpos _target, _unit, _target, true, -1])== 0) && {!(count (lineIntersectsSurfaces [_centerPos, aimpos _target, _unit, _target, true, 1,"FIRE"])== 0) && {!(needreload _unit == 1)}}}) : {_unit playactionnow "stop";_unit setVelocity [0, 0, 0];sleep 0.1;_unit playactionnow "AdjustL";sleep 1;_unit playactionnow "AdjustR";};
			case (!(pose _unit == "Lying") && {(count (lineIntersectsSurfaces [_leanRightPos, aimpos _target, _unit, _target, true, -1])== 0) && {!(count (lineIntersectsSurfaces [_centerPos, aimpos _target, _unit, _target, true,  1,"FIRE"])== 0) && {!(needreload _unit == 1)}}}) : {_unit playactionnow "stop";_unit setVelocity [0, 0, 0];sleep 0.1;_unit playactionnow "AdjustR";sleep 1;_unit playactionnow "AdjustL";};
			default {};
		};
		};
		////Fail safe return
		if ("aadj" in animationstate _unit && {"left" in animationstate _unit}) then {_unit playactionnow "stop";_unit setVelocity [0, 0, 0];sleep 0.2;_unit playactionnow "AdjustR";sleep 0.5};
		if ("aadj" in animationstate _unit && {"right" in animationstate _unit}) then {_unit playactionnow "stop";_unit setVelocity [0, 0, 0];sleep 0.2;_unit playactionnow "AdjustL";sleep 0.5};
		
		_reldir=_unit getreldir getpos _target;
		if ((([_unit, "VIEW",_target] checkVisibility [eyepos _unit, aimpos _target]) > 0 OR ([_unit, "VIEW",_target] checkVisibility [aimpos _unit, eyepos _target]) > 0) && {!isnull _target}) then {
		_anims=RNG_ANIM_Tact;
		} else {	
			if ((time > (_targettimeout - 2)) OR !alive _target) then {
				_anims=RNG_ANIM_Run;
			};
		};
		
		///dodgin
		
		_dodge=false;
		if ((time - _starttime) % 3 > 2.5 && {!isNull _target}) then {
			_targetdir=_target getreldir getpos _unit;
			if (((_targetdir) < 2.55555555555 OR (_targetdir) > 358.555555555 )) then {
				_dodge=true;
			};
		};
		if (isnil "_pos" OR {((_unit distance2D _pos) > 70)} OR _dodge) then {
			_leftpos=lineIntersectsSurfaces [aimPos _unit,(AGLtoASL (_unit getrelpos [20,270])), _unit, objNull, true, 1,"FIRE"];
			_rightpos=lineIntersectsSurfaces [aimPos _unit,(AGLtoASL (_unit getrelpos [20,90])), _unit, objNull, true, 1,"FIRE"];
			switch (true) do {
				case (_dodge) : {_pos = selectrandom [((_leftpos select 0) select 0),((_rightpos select 0) select 0)];};
				case ((((_leftpos select 0) select 0) distance2D  _unit) > (((_rightpos select 0) select 0) distance2d _unit)) : {_pos=((_rightpos select 0) select 0);};
				case ((((_leftpos select 0) select 0) distance2D  _unit) < (((_rightpos select 0) select 0) distance2d _unit)) : {_pos=((_leftpos select 0) select 0);};
				default {};
			};
		};
		if (!isnil "_pos" && {((_unit distance2D _pos) < 70)}) then {
			
			///debug pos - ar1 setPosASL [_pos select 0,_pos select 1,(_pos select 2) + 2.5];
			switch (true) do {
				case ((_unit distance2D _pos)< (1.5 + (vectorMagnitude (velocityModelSpace _unit))*0.25)): {if ((vectorMagnitude (velocityModelSpace _unit)) > 0.2) then {_unit playactionnow "stop";};_unit setVelocity [0, 0, 0];};
				case ((_unit getreldir _pos) < 67.5 && {(_unit getreldir _pos) > 22.5}): {_unit playactionnow (_anims select 0)};
				case ((_unit getreldir _pos) < 342.5 && {(_unit getreldir _pos) > 297.5}): {_unit playactionnow (_anims select 1);};
				case ((_unit getreldir _pos) < 22.5 OR (_unit getreldir _pos) > 342.5): {_unit playactionnow (_anims select 2);};
				case ((_unit getreldir _pos) < 202.5 && {(_unit getreldir _pos) > 157.5}): {_unit playactionnow (_anims select 3);};
				case ((_unit getreldir _pos) < 157.5 && {(_unit getreldir _pos) > 112.5}): {_unit playactionnow (_anims select 4);};
				case ((_unit getreldir _pos) < 247.5 && {(_unit getreldir _pos) > 202.5}): {_unit playactionnow (_anims select 5);};
				case ((_unit getreldir _pos) < 112.5 && {(_unit getreldir _pos) > 67.5}): {_unit playactionnow (_anims select 6);};
				case ((_unit getreldir _pos) < 292.5 && {(_unit getreldir _pos) > 247.5}): {_unit playactionnow (_anims select 7);};
				default {};
			};
			} else {
				_unit playactionnow "stop";
				_unit setVelocity [0, 0, 0];
			};
			if ((time - _starttime) % 1 > 0.5) then {
				if (("tacs" in animationstate _unit OR "run" in animationstate _unit OR "evas" in animationstate _unit) && {((vectorMagnitude (velocityModelSpace _unit)) < 1.5)}) then {_unit playactionnow "stop";_unit setVelocity [0, 0, 0];_type=_types selectRandomWeighted _weight;};
				if ((_unit ammo currentweapon _unit < 2) && {_cancrouch && {!("reload" in (gesturestate _unit))}}) then {
					_stance=["MIDDLE","DOWN"] selectRandomWeighted [1,0.2];
					if (!((unitpos _unit) == _stance)) then {
					_unit playactionnow _stance;
					};
					if (_stance=="MIDDLE") then {
					_unit setunitpos "MIDDLE";
					} else {
						_unit setunitpos "DOWN";
					};
					
				};
			private _randomstop=random 15;
			if (_randomstop > (15 - _skill)) then {_unit playactionnow "stop";_unit setVelocity [0, 0, 0];sleep (_skill/10)};
			};
			if ((animationstate _unit == "amovpercmstpsraswrfldnon" OR animationstate _unit == "amovpknlmstpsraswrfldnon") && {((vectorMagnitude (velocityModelSpace _unit)) < 1)}) then {_unit setVelocity [0, 0, 0];};
	_unit setvariable ["RNG_target", _target];
	} else {_exit=true};
	sleep 0.05;
};
